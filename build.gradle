buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id 'java-library'
    id 'project-report'
    id 'maven-publish'
    id 'idea'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "https://${artifactsRepoName}/releases"
    }
}

apply from: 'dependencies.gradle'

sourceCompatibility = 1.8
targetCompatibility = 1.8

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat = 'full'
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'spg-testing'
            artifactId = 'spg-httpclient'
            version = rootProject.file('VERSION').exists() ? rootProject.file('VERSION').text.trim() : '0.0.0'

            from components.java
        }
    }

    repositories {
        maven {
            credentials(HttpHeaderCredentials.class) {
                name = "Authorization"
                value = "Bearer ${System.getProperty("GITHUB_ACCESS_TOKEN") ?: System.getenv("GITHUB_ACCESS_TOKEN")}"
            }
            authentication {
                register("header", HttpHeaderAuthentication.class)
            }
            url = version.endsWith('SNAPSHOT') ? "https://${artifactsRepoName}/snapshots/" : "https://${artifactsRepoName}/releases/"
        }
    }
}